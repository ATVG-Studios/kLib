plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'maven-publish'
    id 'signing'
}

group 'com.atvgstudios'
version '0.2.1'

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/dokka' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    dokkaRuntime "org.jetbrains.dokka:dokka-fatjar:0.9.18"

    testImplementation "org.jetbrains.kotlin:kotlin-test:1.3.31"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.3.31"
    testImplementation "org.jetbrains.kotlin:kotlin-test-common:1.3.31"
    testImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common:1.3.31"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Specification-Title': "kLib"
        attributes 'Specification-Version': "alpha"
        attributes 'Specification-Vendor': "ATVG-Studios"
        attributes 'Implementation-Title': "kLib"
        attributes 'Implementation-Version': "$version"
        attributes 'Implementation-Vendor': "ATVG-Studios"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    jdkVersion = 8
}


publishing {
    repositories {
        maven {
            url = "$buildDir/repo"
        }
	
    }
    publications {
        maven(MavenPublication) {
	    pom {
                name = 'kLib'
                description = 'kLib is a in Kotlin written Library by ATVG-Studios containing a set of useful Functions, Extensions and Types.'
                url = 'https://klib.atvg-studios.com/'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://gitlab.atvg-studios.at/atvg-studios/kLib/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'atjontv'
                        name = 'Thomas Obernosterer'
                        email = 'thomas.obernosterer@atvg-studios.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://gitlab.atvg-studios.at/atvg-studios/kLib.git'
                    developerConnection = 'scm:git:ssh://gitlab.atvg-studios.at/atvg-studios/kLib.git'
                    url = 'https://gitlab.atvg-studios.at/atvg-studios/kLib.git'
                }
            }
            groupId = 'com.atvgstudios'
            artifactId = 'klib'
            version = "$version"
            from components.kotlin
        }
    }
}

signing {
    sign publishing.publications.maven
}
